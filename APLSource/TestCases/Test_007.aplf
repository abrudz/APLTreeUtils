 R←Test_007(stopFlag batchFlag);A;io;⎕IO;ml;⎕ML;buffer;⎕TRAP;noOf;result;First
⍝ Exercise "Lowercase"
 A←##.APLTreeUtils
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 First←{⎕ML←3 ⋄ ↑⍵}

 :For io :In 0 1
     ⎕IO←io
     :For ml :In 0 1 2 3
         ⎕ML←ml

         →T.FailsIf''≢buffer←A.Lowercase''
         →T.FailsIf'kai jaeger apl software team'≢buffer←A.Lowercase'KAI JAEGER APL Software Team'
         →T.FailsIf'kai jaeger' 'apl software team'≢buffer←A.Lowercase'KAI JAEGER' 'APL Software Team'
         →T.FailsIf'üäößüäö'≢buffer←A.Lowercase'üäößÜÄÖ'
         →T.FailsIf'ü'≢buffer←A.Lowercase'ü'
         →T.FailsIf(,'ü')≢buffer←A.Lowercase,'ü'
         →T.FailsIf'ü'≢buffer←A.Lowercase'Ü'
         →T.FailsIf(,'ü')≢buffer←A.Lowercase,'Ü'
         →T.FailsIf'a'≢buffer←A.Lowercase'A'
         →T.FailsIf(,'a')≢buffer←A.Lowercase,'A'
         →T.FailsIf'∑x²'≢buffer←A.Lowercase'∑x²'
         →T.FailsIf(2 2⍴'aabb')≢buffer←A.Lowercase 2 2⍴'AaBb'

         :If 15>First(//)⎕VFI First'.'A.Split(1+⎕IO)⊃'#'⎕WG' APLVersion'
             →T.FailsIf'' '' '' ''≢,buffer←A.Lowercase''⍬''⍬
         :EndIf

         :Trap 11,16 ⍝ Was 16 in 12.1 and earlier but got 11 in 13.0
             A.Lowercase 1
             →T.FailsIf 1
         :EndTrap

         :Trap 11,16 ⍝ Was 16 in 12.1 and earlier but got 11 in 13.0
             A.Lowercase 1(2 3)
             →T.FailsIf 1
         :EndTrap

         noOf←⌊0.5×⌊⎕WA÷1000
         buffer←(⊂1000000⍴'APL IS FINE '),'A',(noOf)⍴⊂'ABC'   ⍝ This will cause a WS FULL when converted into a matrix
         result←A.Lowercase buffer
         buffer←(⊂1000000⍴'apl is fine '),'a',(noOf⍴⊂'abc')
         →T.FailsIf buffer≢result
     :EndFor
 :EndFor

 R←T._OK
