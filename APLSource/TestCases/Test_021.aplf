 R←Test_021(stopFlag batchFlag);A;IOs;MLs;⎕TRAP;Mix
⍝ Exercise "dtb"
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 A←##.APLTreeUtils
 Mix←{⎕ML←1 ⋄ ↑⍵}
 IOs←0 1
 MLs←0 1 2 3

 :For ⎕IO :In IOs
     :For ⎕ML :In MLs

         →T.FailsIf~0∊⍴A.dtb' '
         →T.FailsIf''≢A.dtb''
         →T.FailsIf'A'≢A.dtb'A'
         →T.FailsIf'APL'≢A.dtb'APL'
         →T.FailsIf' APL  is great'≢A.dtb' APL  is great    '
         →T.FailsIf'APL' '   APL   is great'≢A.dtb'APL' '   APL   is great '
         →T.FailsIf(Mix'APL' '   APL   is great')≢A.dtb Mix'APL' '   APL   is great '

     :EndFor
 :EndFor


 R←T._OK
