 R←Test_006(stopFlag batchFlag);A;io;⎕IO;ml;⎕ML;path;buffer;⎕TRAP
⍝ Exercise "Last"
 A←#.APLTreeUtils
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :For io :In 0 1
     ⎕IO←io
     :For ml :In 0 1 2 3
         ⎕ML←ml

         →FailsIf 0≠⍴buffer←A.Last''
         →FailsIf 0≠⍴buffer←A.Last ⍬
         →FailsIf''≢buffer←A.Last' '
         →FailsIf ⍬≢buffer←A.Last 1
         path←'C:/Buffer/Path1/Path Level 2/filen.ext'
         →FailsIf'ext'≢buffer←A.Last path
         →FailsIf'filen.ext'≢buffer←'/'A.Last path
         →FailsIf(⊂1 2 3)≢buffer←¯1 A.Last 1(2 3)¯1(1 2 3)
         →FailsIf(⊂1 2 3)≢buffer←(⊂¯1 ¯2)A.Last 1(2 3)(¯1 ¯2)(1 2 3)
         →FailsIf(⊂1 2 3)≢buffer←(⊂⍬)A.Last 1(2 3)⍬(1 2 3)
         →FailsIf(⊂'firlefanz')≢buffer←(⊂'')A.Last'qwe' 'z' '' 'firlefanz'
         →FailsIf(⊂'firlefanz')≢buffer←(⊂'a' 'ab')A.Last'qwe' 'z'('a' 'ab')'firlefanz'
         →FailsIf''≢buffer←'-'A.Last path
         →FailsIf ⍬≢buffer←'-'A.Last 1 2 3
         →FailsIf(0⍴path path)≢buffer←'-'A.Last path path
         →FailsIf(0⍴2⍴⊂1 2 3)≢buffer←'-'A.Last 2⍴⊂1 2 3

     :EndFor
 :EndFor

 R←∆OK
