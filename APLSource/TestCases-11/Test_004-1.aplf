 R←Test_004(stopFlag batchFlag);fn;html;⎕TRAP;answer;browser;rideFlag;exe;success
⍝ Exercise "GoToWebPage"
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If batchFlag
     R←∆NoBatchTest
 :Else
     rideFlag←3501⌶⍬
     browser←(1+rideFlag)⊃'your default browser?' 'the RIDE browser?'
     :If ~rideFlag
         ⍞←'Please open a new (=empty) window of your default browser now, then press <enter> '
         {}⍞
     :EndIf

     success←#.APLTreeUtils.GoToWebPage'aplwiki.com'
     →PassesIf success
     :If 0=∆Ask'Do you see http://aplwiki.com in ',browser
         :Return
     :EndIf
     success←#.APLTreeUtils.GoToWebPage'httpS://www.linkedin.com/in/kaijaeger'
     →PassesIf success
     :If 0=∆Ask'Do you see httpS://www.linkedin.com/in/kaijaeger in ',browser
         :Return
     :EndIf

     :If 'Win'≡#.APLTreeUtils.GetOperatingSystem ⍬
         exe←'C:\Program Files (x86)\Google\Chrome\Application\chrome.exe'
         success←exe #.APLTreeUtils.GoToWebPage'httpS://vector.org.uk'
         →PassesIf success
         :If 0=∆Ask'Do you see httpS://vector.org.uk in Chrome?'
             :Return
         :EndIf
     :EndIf

     fn←(¯4↓#.FilesAndDirs.GetTempFilename''),'.html'
     html←'<h1>Homemade</h1><p>This page was created on the fly...</p>'
     #.APLTreeUtils.WriteUtf8File fn html
     success←#.APLTreeUtils.GoToWebPage'file://',fn
     →PassesIf success
     :If 0=∆Ask'Do you see ',fn,' in ',browser
         :Return
     :EndIf
     ⎕DL 0.2
     #.FilesAndDirs.DeleteFile fn

     fn←(¯5↓fn),' with spaces',¯5↑fn
     #.APLTreeUtils.WriteUtf8File fn html
     success←#.APLTreeUtils.GoToWebPage'file://',fn
     →PassesIf success
     :If 0=∆Ask'Do you see ',fn,' (watch the two spaces!) in ',browser
         :Return
     :EndIf
     #.FilesAndDirs.DeleteFile fn

     :If 'Mac'≡#.APLTreeUtils.GetOperatingSystem ⍬
         success←'Safari'#.APLTreeUtils.GoToWebPage'aplwiki.com'
         →PassesIf success
         :If 0=∆Ask'Do you see http://aplwiki.com in Safari'
             :Return
         :EndIf
     :EndIf

     R←∆OK

 :EndIf
