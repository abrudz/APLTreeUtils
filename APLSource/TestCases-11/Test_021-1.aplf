 R←Test_021(stopFlag batchFlag);A;IOs;MLs;⎕TRAP;Mix
⍝ Exercise "dtb"
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 A←#.APLTreeUtils
 Mix←{⎕ML←1 ⋄ ↑⍵}
 IOs←0 1
 MLs←0 1 2 3

 :For ⎕IO :In IOs
     :For ⎕ML :In MLs

         →FailsIf~0∊⍴A.dtb' '
         →FailsIf''≢A.dtb''
         →FailsIf'A'≢A.dtb'A'
         →FailsIf'APL'≢A.dtb'APL'
         →FailsIf' APL  is great'≢A.dtb' APL  is great    '
         →FailsIf'APL' '   APL   is great'≢A.dtb'APL' '   APL   is great '
         →FailsIf(Mix'APL' '   APL   is great')≢A.dtb Mix'APL' '   APL   is great '

     :EndFor
 :EndFor


 R←∆OK
