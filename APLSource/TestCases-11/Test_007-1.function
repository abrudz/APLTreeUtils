 R←Test_007(stopFlag batchFlag);A;io;⎕IO;ml;⎕ML;buffer;⎕TRAP;noOf;result;First
⍝ Exercise "Lowercase"
 A←#.APLTreeUtils
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 First←{⎕ML←3 ⋄ ↑⍵}

 :For io :In 0 1
     ⎕IO←io
     :For ml :In 0 1 2 3
         ⎕ML←ml

         →FailsIf''≢buffer←A.Lowercase''
         →FailsIf'kai jaeger apl software team'≢buffer←A.Lowercase'KAI JAEGER APL Software Team'
         →FailsIf'kai jaeger' 'apl software team'≢buffer←A.Lowercase'KAI JAEGER' 'APL Software Team'
         →FailsIf'üäößüäö'≢buffer←A.Lowercase'üäößÜÄÖ'
         →FailsIf'ü'≢buffer←A.Lowercase'ü'
         →FailsIf(,'ü')≢buffer←A.Lowercase,'ü'
         →FailsIf'ü'≢buffer←A.Lowercase'Ü'
         →FailsIf(,'ü')≢buffer←A.Lowercase,'Ü'
         →FailsIf'a'≢buffer←A.Lowercase'A'
         →FailsIf(,'a')≢buffer←A.Lowercase,'A'
         →FailsIf'∑x²'≢buffer←A.Lowercase'∑x²'
         →FailsIf(2 2⍴'aabb')≢buffer←A.Lowercase 2 2⍴'AaBb'

         :If 15>First(//)⎕VFI First'.'A.Split(1+⎕IO)⊃'#'⎕WG' APLVersion'
             →FailsIf'' '' '' ''≢,buffer←A.Lowercase''⍬''⍬
         :EndIf

         :Trap 11,16 ⍝ Was 16 in 12.1 and earlier but got 11 in 13.0
             A.Lowercase 1
             →FailsIf 1
         :EndTrap

         :Trap 11,16 ⍝ Was 16 in 12.1 and earlier but got 11 in 13.0
             A.Lowercase 1(2 3)
             →FailsIf 1
         :EndTrap

         noOf←⌊0.5×⌊⎕WA÷1000
         buffer←(⊂1000000⍴'APL IS FINE '),'A',(noOf)⍴⊂'ABC'   ⍝ This will cause a WS FULL when converted into a matrix
         result←A.Lowercase buffer
         buffer←(⊂1000000⍴'apl is fine '),'a',(noOf⍴⊂'abc')
         →FailsIf buffer≢result
     :EndFor
 :EndFor

 R←∆OK
